`timescale 1ns / 1ps

module video_pattern #(
    parameter WIDTH  = 640,
    parameter H_FP   = 16,
    parameter H_PW   = 96,
    parameter H_BP   = 48,
    parameter H_TOT  = WIDTH + H_FP + H_PW + H_BP,
    parameter H_bits = $clog2(H_TOT),
    
    parameter HEIGHT  = 480,
    parameter V_FP    = 10,
    parameter V_PW    = 2,
    parameter V_BP    = 33,
    parameter V_TOT   = HEIGHT + V_FP + V_PW + V_BP,
    parameter V_bits  = $clog2(V_TOT)
    )
    (
    input wire iClk,
    input wire [H_bits-1:0] iCountH, 
    input wire [V_bits-1:0] iCountV,
    
    input wire [9:0] iShapeX, 
    input wire [9:0] iShapeY,
    input wire [9:0] iShapeSize,
    
    input wire [1:0]  iShapeColor,   
    input wire [3:0]  iCoinsActive,
    
    output wire [7:0] oGreen, oBlue, oRed,
    output wire oActive
    );

    //Walls
    localparam W_THICK = 20;
    wire is_border, is_wall_1, is_wall_2, is_wall_3, is_wall_4, is_wall_5, is_wall;
    assign is_border = (iCountH < W_THICK) | (iCountH >= WIDTH - W_THICK) | (iCountV < W_THICK) | (iCountV >= HEIGHT - W_THICK);
    assign is_wall_1 = (iCountH >= 150 && iCountH < 170) && (iCountV < 350);
    assign is_wall_2 = (iCountH >= 450 && iCountH < 470) && (iCountV > 100);
    assign is_wall_3 = (iCountH >= 170 && iCountH < 300) && (iCountV >= 200 && iCountV < 220);
    assign is_wall_4 = (iCountH >= 320 && iCountH < 450) && (iCountV >= 300 && iCountV < 320);
    assign is_wall_5 = (iCountH >= 280 && iCountH < 340) && (iCountV >= 100 && iCountV < 140);
    assign is_wall = is_border | is_wall_1 | is_wall_2 | is_wall_3 | is_wall_4 | is_wall_5;

    //coins
    reg [9:0] cX [0:3];
    reg [9:0] cY [0:3];
    
    initial begin
        cX[0] = 50;  cY[0] = 50;   
        cX[1] = 50;  cY[1] = 400;  
        cX[2] = 580; cY[2] = 50;   
        cX[3] = 580; cY[3] = 400;  
    end

    reg is_coin_pixel;
    integer i;
    always @(*) begin
        is_coin_pixel = 0;
        for (i=0; i<4; i=i+1) begin
            if (iCoinsActive[i] && 
                iCountH >= cX[i] && iCountH < cX[i] + 15 && 
                iCountV >= cY[i] && iCountV < cY[i] + 15) begin
                is_coin_pixel = 1;
            end
        end
    end

    //player 
    wire in_square;
    assign in_square = (iCountH >= iShapeX && iCountH < (iShapeX + iShapeSize)) && 
                       (iCountV >= iShapeY && iCountV < (iShapeY + iShapeSize));

    assign oActive = (iCountH < WIDTH && iCountV < HEIGHT);

    reg [7:0] r, g, b;
    always @(*) begin
        r = 0; g = 0; b = 0;
        if (oActive) begin
            if (is_wall) begin
                b = 255; // all walls are blue
            end else if (is_coin_pixel) begin
                r = 255; g = 255; // set coins yellov
            end else if (in_square) begin
                case (iShapeColor[1:0]) 
                    2'b00: r = 255; 
                    2'b01: g = 255; 
                    2'b10: begin r = 255; b = 255; end 
                    default: begin r = 255; g = 255; b = 255; end 
                endcase
            end
        end
    end
    assign oRed = r;
    assign oGreen = g;
    assign oBlue = b;
endmodule
